#                                               -*- Autoconf -*-

# $Id: configure.in,v 2.0 2004/09/13 19:44:25 alexios Exp $
#
# $Log: configure.in,v $
# Revision 2.0  2004/09/13 19:44:25  alexios
# Stepped version to recover CVS repository after near-catastrophic disk
# crash.
#
# Revision 1.14  2004/07/15 14:44:34  alexios
# Changed instances of CFLAGS to AM_CFLAGS so as not to override user
# settings.
#
# Revision 1.13  2004/05/22 19:24:06  alexios
# Added bbsuserdel.
#
# Revision 1.12  2004/05/21 20:15:08  alexios
# Added the bbscleanup directory.
#
# Revision 1.11  2004/05/03 05:27:13  alexios
# Numerous changes to implement the new build infrastructure.
#
# Revision 1.10  2003/12/24 18:43:15  alexios
# Added SOVERSION variable used when building shared objects; added
# checks for curses, ncurses and friends (headers and libraries). For
# the time being, only true curses and ncurses libraries are checked
# for. Slang with curses emulation isn't. Added MetaBBS support,
# including switches to enable it. It is currently disabled due to build
# issues and is no longer needed in its present, RPC-based, form. Added
# mbk directory to simplify dependency graph where more than one
# subdirectory depends on the same message block or header file. Added
# more source subdirectories.
#
# Revision 1.9  2003/12/22 16:45:05  alexios
# Numerous changes in directory setup, AC_OUTPUT variables et cetera.
#
# Revision 1.8  2003/09/30 15:11:34  alexios
# Various changes to account for updated directory structure and new code.
#
# Revision 1.7  2003/09/29 21:37:32  alexios
# Renamed src/ directory to lib/ to accommodate more source code in this
# tree.
#
# Revision 1.6  2003/09/28 22:23:59  alexios
# Added conditional generation of documentation (via doxygen detection)
# and locale directory.
#
# Revision 1.5  2003/09/28 13:14:32  alexios
# Added gettext tests and support.
#
# Revision 1.4  2003/08/15 20:30:54  alexios
# Added magic to get the version from debian/changelog, so it only needs
# to be changed in one place, and it's done automatically too.
#
# Revision 1.3  2003/08/15 20:25:55  alexios
# Stepped version.
#
# Revision 1.2  2003/08/15 20:07:59  alexios
# Added enough information and variables for megistos-config.
#
# Revision 1.1  2003/08/15 18:06:56  alexios
# Initial revision.
#

dnl Process this file with autoconf to produce a configure script.
AC_INIT(libmegistos/audit.c)

dnl Magic to obtain the package name and version from
dnl debian/changelog, where it should reside.

AC_MSG_CHECKING(package name and version)
package=`head -1 $srcdir/debian/changelog | sed -e 's/^ \+//g' -e 's/ \+.*$//'`
ver=`grep urgency= $srcdir/debian/changelog |head -1|sed -e 's/^.*(\(.*\)).*$/\1/'`
AC_MSG_RESULT(${package}-${ver})

dnl The version information for shared objects build against this libmegistos.

SOVERSION=3:5:1
AC_SUBST(SOVERSION)

dnl A kludge
exec_prefix=$prefix

dnl Every other copy of the package version number gets its value from
dnl here (BUT NOT the files in debian/)

AM_INIT_AUTOMAKE($package, $ver)

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AM_CONFIG_HEADER(bbsconfig.h)

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
dnl AC_PROG_RANLIB

dnl Check for doxygen
AC_PATH_PROG([DOXYGEN],[doxygen])
AM_CONDITIONAL(HAVE_DOXYGEN,test .$DOXYGEN != .)

dnl Check for indent
AC_PATH_PROG([INDENT],[indent])
AM_CONDITIONAL(HAVE_INDENT,test .$INDENT != .)
AC_SUBST(INDENT)

dnl Use libtool to make a shared library (with ltdl support)
AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_SUBST(LTDLINCL)
AC_SUBST(LIBLTDL)
AC_LIBLTDL_INSTALLABLE
AC_PROG_LIBTOOL
AC_CONFIG_SUBDIRS(libltdl)
AM_CFLAGS="$CFLAGS $LIBLINCL"
LDFLAGS="$LDFLAGS $LIBLTDL"

dnl Check for i18n
AM_GNU_GETTEXT
AM_GNU_GETTEXT_VERSION(0.12.1)

dnl Users and group (not configurable for now)
TOPLEVEL_USER=megistos
AC_SUBST(TOPLEVEL_USER)
dnl AC_DEFINE_UNQUOTED(TOPLEVEL_USER ,"$TOPLEVEL_USER",[The toplevel username.])

TOPLEVEL_GROUP=megistos
AC_SUBST(TOPLEVEL_GROUP)
dnl AC_DEFINE_UNQUOTED(TOPLEVEL_GROUP ,"$TOPLEVEL_GROUP",[The toplevel group name.])

# Check for Curses, but don't link against it -- instead, save the
# linker flags for use by whatever program needs it (using the
# Automake template template/curses)

LIBS_SAVE="$LIBS"
LIBS=""
AC_CHECK_LIB(ncurses,can_change_color,,
  AC_CHECK_LIB(curses,attr_on,,AC_MSG_ERROR([*** ncurses or curses is required but neither was found.])))
CURSES_LIBS="$LIBS"
AC_SUBST(CURSES_LIBS)
LIBS="$LIBS_SAVE"

dnl Check for typhoon
LIBS_SAVE="$LIBS"
LIBS=""
AC_CHECK_LIB(typhoon,d_fillnew,,AC_MSG_ERROR([*** typhoon is required to build Megistos.]))
TYPHOON_LIBS="$LIBS"
AC_SUBST(TYPHOON_LIBS)
LIBS="$LIBS_SAVE"
AC_PATH_PROG([DDLP],[ddlp])
AC_CHECK_HEADERS(libtyphoon/typhoon.h,,AC_MSG_ERROR([*** libtyphoon/typhoon.h is not available. Is the development package installed?]))

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(\
	arpa/telnet.h \
	errno.h \
	dlfcn.h \
	fcntl.h \
	getopt.h \
	grp.h \
	limits.h \
	malloc.h \
	memory.h \
	netinet/in.h \
	paths.h \
	pwd.h \
	rx.h \
	regex.h \
	rpc/rpc.h \
	rpc.h \
	setjmp.h \
	sgtty.h \
	signal.h \
	stdarg.h \
	stdint.h \
	string.h \
	strings.h \
	sys/errno.h \
	sys/file.h \
	sys/filio.h \
	sys/int_types.h \
	sys/ioctl.h \
	sys/ipc.h \
	sys/msg.h \
	sys/param.h \
	sys/resource.h \
	sys/sem.h \
	sys/shm.h \
	sys/signal.h \
	sys/socket.h \
	sys/stat.h \
	sys/stream.h \
	sys/time.h \
	sys/tty.h \
	sys/types.h \
	sys/un.h \
	sys/varargs.h \
	sys/vt.h \
	sys/wait.h \
	syslog.h \
	telnet.h \
	termcap.h \
	termio.h \
	unistd.h \
	utime.h \
	utmp.h \
	varargs.h \
	zlib.h \
	zlib/zlib.h \
	ncurses.h \
	curses.h \
	curses/curses.h \
	curses/ncurses.h \
	ncurses/ncurses.h \
	ncurses/curses.h
)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_STRUCT_ST_RDEV
AC_STRUCT_TM
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T

# Checks for library functions.

AC_FUNC_CHOWN
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_FORK
AC_FUNC_MEMCMP
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_SETPGRP
AC_FUNC_STRFTIME
AC_FUNC_UTIME_NULL
AC_FUNC_VPRINTF

AC_HEADER_STDC
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(
	atexit \
	bzero \
	chown \
	getcwd \
	gethostname \
	getpt \
	gettimeofday \
	malloc \
	memcpy \
	memset \
	mkdir \
	mkfifo \
	mktime \
	putenv \
	realloc \
	rmdir \
	select \
	socket \
	stat \
	strchr \
	strcspn \
	strdup \
	strerror \
	strrchr \
	strspn \
	strstr \
	uname \
	usleep \
	vhangup \
	setenv \
)

if test x$datadir != xNONE ;
then
	p=$datadir
else
	p=$prefix
fi

BASENAME=megistos
AC_SUBST(BASENAME)
AC_DEFINE_UNQUOTED(BASENAME,"$BASENAME",[The default system directory name.])

LOCALEDIR=$p/share/locale
AC_SUBST(LOCALEDIR)
dnl AC_DEFINE_UNQUOTED(LOCALEDIR,"$LOCALEDIR",[The system locale directory.])

INSTANCEDIR=${localstatedir}/lib/${BASENAME}
AC_SUBST(INSTANCEDIR)

dnl PKGDATADIR=$datadir/$BASENAME
PKGDATADIR=$libdir/$BASENAME
AC_SUBST(PKGDATADIR)

PKGLIBDIR=$libdir/$BASENAME
AC_SUBST(PKGLIBDIR)

PKGINCLUDEDIR=$includedir/$BASENAME
AC_SUBST(PKGINCLUDEDIR)

VIRGIN=virgin
VIRGINDIR=$VIRGIN
AC_SUBST(VIRGINDIR)

dnl VIRGININSTANCE=$p/$BASENAME/$VIRGIN
VIRGININSTANCE=$PKGLIBDIR/$VIRGIN
AC_SUBST(VIRGININSTANCE)

AM_CFLAGS="$AM_CFLAGS -Wall"

dnl This is the main library directory. Its name is user-defined for
dnl flexibility (and because it kept changing for a while).

LIBMEGISTOS=libmegistos
AC_SUBST(LIBMEGISTOS)

dnl If the build directory is different from the source directory,
dnl copy the virgin bbs skeleton over in anticipation of compiling
dnl some of the message blocks.

AC_MSG_CHECKING(build directory)
AC_MSG_RESULT($builddir)
AC_MSG_CHECKING(source directory)
AC_MSG_RESULT($srcdir)
AC_MSG_CHECKING(build and source directories coincide)
if test x$abs_top_builddir != x$abs_top_srcdir ;
then
	AC_MSG_RESULT(no)
	AC_MSG_NOTICE([Instantiating $VIRGINDIR])
	dnl Use tar (we may not have GNU cp)
	(cd $abs_top_srcdir; tar -cf - virgin) | \
	    (cd $abs_top_builddir; umask 000; tar -xvf -)
else
	AC_MSG_RESULT(yes)
fi

AC_MSG_CHECKING(for all msg files)
(cd mbk; /bin/sh ./mkdeps.sh >.dep)
AC_MSG_RESULT(done)

dnl Options

AC_MSG_CHECKING(whether to compile with MetaBBS support)
AC_ARG_ENABLE(metabbs,[  --enable-metabbs        Enable MetaBBS extensions (default no)],,enable_metabbs=no)
if test x$enable_metabbs = xyes; then
	AC_MSG_RESULT(yes)
	AC_SUBST(HAVE_METABBS)
	AC_DEFINE(HAVE_METABBS,1,[Set if the MetaBBS pooling/syndication extension is enabled.])
else
	AC_MSG_RESULT(no)
fi
AM_CONDITIONAL(HAVE_METABBS,test x$enable_metabbs = xyes)


dnl Output Makefiles and other autogenerated files.

AC_OUTPUT([
	Makefile
	libmegistos/Makefile
	libmegistos/megistos-config
	libmegistos/gcs_builtin/Makefile
	mbk/Makefile
	intl/Makefile
	po/Makefile.in
	m4/Makefile
	doc/Makefile
 	src/Makefile

	src/modules/Makefile
	src/modules/account/Makefile
	src/modules/adventure/Makefile
	src/modules/adventure/infoint/Makefile
	src/modules/bulletins/Makefile
	src/modules/bulletins/offline.bulletins/Makefile
	src/modules/cookie/Makefile
	src/modules/cookie/gcs_cookie/Makefile

	src/modules/emailclubs/Makefile
	src/modules/emailclubs/bbsmail/Makefile
	src/modules/emailclubs/netclub/Makefile
	src/modules/emailclubs/offline.mail/Makefile
 	src/modules/emailclubs/mailfixup/Makefile 

	src/modules/files/Makefile
	src/modules/graffiti/Makefile
	src/modules/graffiti/offline.graffiti/Makefile

	src/modules/mailer/Makefile
	src/modules/mailer/plugins/Makefile

	src/modules/news/Makefile
	src/modules/news/offline.news/Makefile
	src/modules/notify/Makefile
	src/modules/registry/Makefile
	src/modules/registry/gcs_registry/Makefile
	src/modules/remsys/Makefile
	src/modules/remsys/gcs_remsys/Makefile
	src/modules/remsys/classed/Makefile
	src/modules/remsys/eventman/Makefile
 	src/modules/remsys/console/Makefile 
	src/modules/stats/Makefile

	src/modules/telecon/Makefile
	src/modules/telecon/mkaction/Makefile
	src/modules/telecon/mkplugin/Makefile
	src/modules/telecon/plugins/Makefile
	src/modules/telecon/plugins/bjack/Makefile
	src/modules/telecon/plugins/telecon.bg/Makefile
	src/modules/telecon/plugins/telecon.test/Makefile

 	src/system/Makefile

	src/system/bbscleanup/Makefile
	src/system/bbsdeluser/Makefile
	src/system/bbsgetty/Makefile
	src/system/bbslogin/Makefile
	src/system/bbslogout/Makefile

 	src/system/daemons/Makefile
 	src/system/daemons/bbsd/Makefile
 	src/system/daemons/bbsinit/Makefile
 	src/system/daemons/bbslockd/Makefile
 	src/system/daemons/emud/Makefile
 	src/system/daemons/statd/Makefile
	src/system/daemons/msgd/Makefile

 	src/system/events/Makefile
 	src/system/events/bbsshutdown/Makefile
 	src/system/events/linechange/Makefile

	src/system/menuman/Makefile
	src/system/menuman/gcs_go/Makefile
	src/system/menuman/mkmtree/Makefile

	src/system/metabbs/Makefile
	src/system/metabbs/client/Makefile
	src/system/metabbs/server/Makefile

	src/system/protocols/Makefile
	src/system/protocols/asciiupload/Makefile
	src/system/protocols/attach-to-email/Makefile
	src/system/protocols/editupload/Makefile
	src/system/protocols/listdownload/Makefile
	src/system/protocols/cat-and-remove/Makefile
	src/system/protocols/gzipview/Makefile
	src/system/protocols/uuencode-and-remove/Makefile
	src/system/protocols/view-and-remove/Makefile

	src/system/signup/Makefile

	src/tools/Makefile
	src/tools/bbsdialog/Makefile
	src/tools/checksup/Makefile
	src/tools/lined/Makefile
	src/tools/updown/Makefile
	src/tools/vised/Makefile

 	src/utils/Makefile
 	src/utils/megistos-admin/Makefile 
 	src/utils/fixemail/Makefile 
 	src/utils/mkchan/Makefile
 	src/utils/mkxlation/Makefile
 	src/utils/sysved/Makefile 
 	src/utils/connection-helpers/Makefile
])

dnl Disabled or leftover makefiles:

dnl 	src/system/daemons/rpc.metabbs/Makefile
dnl	src/modules/gallups/Makefile
dnl	src/modules/gallups/gsc/Makefile


dnl Print out the configuration.

cat <<EOF

Build environment

Compiler (CC):            $CC
Compiler flags (CFLAGS):  $AM_CFLAGS $CFLAGS
Linker (LD):              $LD
Linker flags (LDFLAGS):   $LDFLAGS
Build directory:          $abs_top_builddir
Source directory:         $abs_top_srcdir
Curses:                   $CURSES_LIBS
Typhoon:                  $TYPHOON_LIBS

Summary of settings

Installation prefix:      $prefix
Execution prefix:         $exec_prefix
Data directory:           $PKGDATADIR
BBS Instance directory:	  $INSTANCEDIR
Library directory:        $PKGLIBDIR
Virgin instance:          $VIRGINDIR
Include directory:        $PKGINCLUDEDIR

You can now run:

\$ make

EOF
